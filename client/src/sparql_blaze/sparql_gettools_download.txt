PREFIX schema:  <https://schema.org/>
PREFIX schemaold:  <http://schema.org/>
select DISTINCT ?rrs ?name (GROUP_CONCAT(DISTINCT ?curl1; SEPARATOR=", ") AS ?curl)  (GROUP_CONCAT(DISTINCT ?landingPage1; SEPARATOR=", ") AS ?landingPage)
(GROUP_CONCAT(DISTINCT ?altName1; SEPARATOR=", ") AS ?altName) ?version ?identifier ?description
WHERE                    {
    graph <${g}>
      {
        {
          ?s schemaold:distribution|schema:distribution ?dist .
          ?dist  schemaold:encodingFormat|schema:encodingFormat ?type .
          ?dist schemaold:contentUrl|schema:contentUrl|schema:url|schemaold:url ?curl1
        }
        UNION {
          VALUES (?dataset) { ( schema:Dataset ) ( schemaold:Dataset ) }
          ?s a ?dataset .
          ?s  schemaold:encodingFormat|schema:encodingFormat ?type .
          }
     }
     BIND (str(?type) as ?label)
     SERVICE <http://132.249.238.169:8080/fuseki/ecrr/query> {
      GRAPH <http://earthcube.org/gleaner-summoned>
       {
         { ?rrs schema:supportingData ?df.
              ?df schema:encodingFormat  ?label ;
                  schema:position "input".
              ?rrs schema:name ?name;
                   schema:description ?description .

               ?rrs schema:subjectOf/schema:url ?landingPage1
               OPTIONAL{ ?rrs schema:alternateName ?altName1 }
               OPTIONAL{ ?rrs schema:version ?version }
                OPTIONAL{ ?rrs schema:identifier ?identifier }
      }

   }
  }
}
 GROUP BY ?rrs ?name   ?description ?version  ?identifier
