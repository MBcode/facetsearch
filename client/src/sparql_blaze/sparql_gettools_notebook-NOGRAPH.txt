# no graph parameter
   
PREFIX schema: <https://schema.org/>
PREFIX schemaold: <http://schema.org/>
PREFIX ecrro: <http://cor.esipfed.org/ont/earthcube/>

SELECT DISTINCT ?dataname ?type ?APItemplate ?appname ?nburl
WHERE
{
      {
#is there a distribution with a schema:serviceType property
       ?s schemaold:distribution|schema:distribution ?dist ;
            schemaold:name|schema:name ?dataname  .
       ?dist  schemaold:serviceType|schema:serviceType ?type ;
#this is the template for directly accessing the WebAPI
           schemaold:potentialAction/schemaold:target/schemaold:urlTemplate|schema:potentialAction/schema:target/schema:urlTemplate ?APItemplate.
      }
   BIND (str(?type) as ?pval)
	{
#check if there are any software applications the implement the WebAPI interface
#  currently, we're looking mostly for notebooks.
            {?rrs a schema:SoftwareApplication ;
                   schema:name ?appname ;
				   schema:additionalProperty ?prop.
     OPTIONAL  {
              ?rrs    schema:description ?description ;
					schema:potentialAction/schema:target/schema:urlTemplate ?nburl.
                }
              ?prop schema:name ?pname ;
#this is the critical match-- the serviceType for the data distribution has to match the
#   interface specification for the Application.
			    schema:value/schema:name ?pval.
              filter  (?pname = 'Interface specification') }
            }
}
